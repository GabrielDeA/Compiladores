#Options
GenerateScanner = true
GenerateParser = true
Language = Java
ScannerName = Lexico
ParserName = Sintatico
SemanticName = Semantico
ScannerCaseSensitive = true
ScannerTable = Full
Input = Stream
Parser = LL
#RegularDefinitions

int: 0|[1-9][0-9]*
float: {int}"."(([0-9]*[1-9])|0)
string: \" ([^ \n \" \\ %]| % (i|f|s|b))* \"
pre: _i|_f|_s|_b
#Tokens
// EQUIPE 02: Gabriel de Antoni Santos, Thomas Leon Rodrigues
// NOTA: 7,4


id: {pre}( [a-z] ( [a-z] | [0-9] | ( [A-Z] ( [a-z] | [0-9] ) ) ) * [A-Z] ? | [A-Z] ( ( [a-z] | [0-9] ) [A-Z] ? ) * )

// especificar

// cte_int
constanteI: {int}

// cte_float
constanteF: {float}

// cte_string
constanteS: {string}

// pal_res
palavra_reservada: [a-z]+

// do else false fun if in main out repeat true while
do = palavra_reservada: "do"
else = palavra_reservada: "else"
false = palavra_reservada: "false"
fun = palavra_reservada: "fun"
if = palavra_reservada: "if"
in = palavra_reservada: "in"
main = palavra_reservada: "main"
out = palavra_reservada: "out"
repeat = palavra_reservada: "repeat"
true = palavra_reservada: "true"
while = palavra_reservada: "while"

//simbolos especiais

//  & | ! , ; = : ( ) { } == != < > + - * /
"&" 
"/"
"!"
","
";"
"="
":"
"("
")"
"{"
"}"
"=="
"!="
"<"
">"
"+"
"-"
"*"
"/"
"|"

//caracteres de formataï¿½ï¿½o: reconhecer e ignorar
:[\s\t\n]

//comentarios de bloco de linha: reconhecer e ignorar
:!"#"[^\n]*
:!"[" [ ^ "[" "]" ] * "]"

#NonTerminals
<program>
<lista_instrucoes>
<lista_instrucoes1>
<instrucao>
<instrucao1>

<lista_id>
<lista_id1>
<valor>


<cmd>
<cmd_entrada>
<cmd_atribuicao>
<lista_entrada>
<lista_entrada1>
<lista_entrada2>
<lista_entrada3>
<cmd_saida>
<lista_expressoes>
<lista_expressoes1>
<cmd_selecao>
<cmd_selecao1>
<cmd_repeticao>
<expressao>
<expressao_>
<elemento>
<relacional>
<relacional_>
<operador_relacional>
<aritmetica>	
<aritmetica_>
<termo>	
<termo_>	
<fator>				
<lista_cmd>
<lista_cmd1>
#Grammar

<program> ::= #100 fun main "{" <lista_instrucoes> "}" #101 ;

<lista_instrucoes> ::= <instrucao> <lista_instrucoes1> ;
<lista_instrucoes1> ::= î | <lista_instrucoes> ;


<instrucao> ::=  <lista_id> <instrucao1> ";" | <cmd_entrada> ";"| <cmd_saida> ";"| <cmd_selecao> ";"| <cmd_repeticao> ";" ;
<instrucao1> ::=  ":" <valor> #126| #127 î | "=" <expressao> #128 ;



<lista_id> ::= id #125 <lista_id1>;
<lista_id1> ::=  î | "," <lista_id>;

<valor> ::= constanteI | constanteF | constanteS | true | false ;

<cmd> ::= <cmd_atribuicao> ";"| <cmd_entrada> ";"| <cmd_saida> ";"| <cmd_selecao> ";"| <cmd_repeticao> ";" ;

<cmd_atribuicao> ::= <lista_id> "=" <expressao> #128 ;
<cmd_entrada> ::= in "(" <lista_entrada> ")" ;

<lista_entrada> ::= constanteS #130 <lista_entrada1> | <lista_id> #129 <lista_entrada2> ;


<lista_entrada1> ::= "," <lista_id> #129 <lista_entrada3> ;

<lista_entrada3> ::= î |  ";" <lista_entrada>;

<lista_entrada2> ::= î | ";" <lista_entrada> ;

<cmd_saida> ::= out "(" <lista_expressoes> ")" ;

<lista_expressoes> ::= <expressao> #102 <lista_expressoes1> ;
<lista_expressoes1> ::=  î| "," <lista_expressoes>;

<cmd_selecao> ::= if "(" <expressao> ")" #118 "{" <lista_cmd> "}" <cmd_selecao1> #119 ;
<cmd_selecao1> ::= î| #120 else "{" <lista_cmd> "}" ;

<lista_cmd> ::= <cmd> <lista_cmd1> ;
<lista_cmd1> ::= î| <lista_cmd> ;
<cmd_repeticao> ::= #121 while "(" <expressao> ")" #122 do "{" <lista_cmd> "}" #123 | #121 repeat "{" <lista_cmd> "}" while "(" <expressao> ")" #124 ;



<expressao>			::= <elemento> <expressao_>  ;
<expressao_>			::= î | "&" <elemento> #103 <expressao_> | "|" <elemento> #104 <expressao_> ;
<elemento>			::= <relacional> | true #105 | false #106 | "!" <elemento> #107 ;

<relacional>			::= <aritmetica> <relacional_> ;
<relacional_>			::= î | <operador_relacional> #108 <aritmetica> #109 ;
<operador_relacional>	::= "==" | "!=" | "<" | ">" ;

<aritmetica>			::= <termo> <aritmetica_>  ;
<aritmetica_>			::= î | "+" <termo> #110 <aritmetica_> | "-" <termo> #111 <aritmetica_> ;

<termo>			::= <fator> <termo_> ;
<termo_>			::= î | "*" <fator> #112 <termo_> | "/" <fator> #113 <termo_> ;

<fator>			::= id #131 | 
			    	    constanteI #114 | 
		   	    	    constanteF #115 | 
			    	    constanteS #116 | 
		   	    	    "(" <expressao> ")" |
	    	  	    	    "+" <fator> | 
	    		    	    "-" <fator> #117 ;



